
--- Lamport's Bakery protocol in Maude.
--- From: All About Maude - A High-Performance Logical Framework. 
fmod BAKERY-SYNTAX is 
  sort Name .
  op 0 : -> Name [ctor] .
  op s : -> Name [ctor] .
  op __ : Name Name -> Name [ctor comm assoc id: 0] .

  sorts ModeIdle ModeWait ModeCrit Mode . 
  subsorts ModeIdle ModeWait ModeCrit < Mode . 
  sorts ProcIdle ProcWait Proc ProcIdleSet ProcWaitSet ProcSet . 
  subsorts ProcIdle < ProcIdleSet . 
  subsorts ProcWait < ProcWaitSet . 
  subsorts ProcIdle ProcWait < Proc < ProcSet . 
  subsorts ProcIdleSet < ProcWaitSet < ProcSet . 
  
  op idle : -> ModeIdle . 
  op wait : Name -> ModeWait . 
  op crit : Name -> ModeCrit . 
  op `[_`] : ModeIdle -> ProcIdle . 
  op `[_`] : ModeWait -> ProcWait . 
  op `[_`] : Mode -> Proc . 
  op none : -> ProcIdleSet . 
  op __ : ProcIdleSet ProcIdleSet -> ProcIdleSet [assoc comm id: none] . 
  op __ : ProcWaitSet ProcWaitSet -> ProcWaitSet [assoc comm id: none] .
  op __ : ProcSet ProcSet -> ProcSet [assoc comm id: none] .

  sort Conf . 
  op _;_;_ : Name Name ProcSet -> Conf .
endfm


mod BAKERY is
  protecting BAKERY-SYNTAX . 
  
  var PS : ProcSet .  vars N M : Name . 
  
  rl [wake] : N ; M ; [idle] PS    => s N ; M ; [wait(N)] PS .
  rl [crit] : N ; M ; [wait(M)] PS => N ; M ; [crit(M)] PS .
  rl [exit] : N ; M ; [crit(M)] PS => N ; s M ; [idle] PS .
endm


load symbolic-checker

--- prop definitions for bakery
(mod BAKERY-SATISFACTION1 is
  pr BAKERY .
  pr SYMBOLIC-CHECKER .

  subsort Conf < State .

  ops ever-wait? ever-crit? : -> Prop . 

  vars N M : Name . vars PS : ProcSet . 
  
  eq s N ; M ; PS |= ever-wait? = true [variant] . 
  eq 0 ; M ; PS    |= ever-wait? = false [variant] . 
  eq N ; s M ; PS |= ever-crit? = true [variant] .
  eq N ; 0 ; PS    |= ever-crit? = false [variant] . 
endm)

set verbose on .

*** true
(lfmc N ; N ; [idle] [idle] |= [] (ever-wait? -> <> ever-crit?) .)

*** deadlock counterexample
(lfmc N ; M ; IS:ProcIdleSet |= [] (ever-wait? -> <> ever-crit?) .)

*** spurious counterexample
(lfmc N ; M ; WS:ProcWaitSet |= [] (ever-wait? -> <> ever-crit?) .)


(mod BAKERY-SATISFACTION2 is
  pr BAKERY .
  pr SYMBOLIC-CHECKER .

  subsort Conf < State .

  ops ex? ini : -> Prop . 

  var WS : ProcWaitSet . var IS : ProcIdleSet . var PS : ProcSet . 
  vars N M M1 M2 : Name .

--- NOTE: ProcIdleSet < ProcWaitSet
  eq N ; M ; WS |= ex? = true [variant] .
  eq N ; M ; [crit(M1)] WS |= ex? = true [variant] .
  eq N ; M ; [crit(M1)] [crit(M2)] PS |= ex? = false [variant] .
endm)

*** no counterexample
(lmc [10] N:Name ; N:Name ; [idle] [idle] |= [] ex? .)

*** true
(lfmc N:Name ; N:Name ; [idle] [idle] |= [] ex? .)

*** false
(lfmc N ; M ; WS |= [] ex?  .)

*** false (infinite graph, but a counterexample exists)
(lfmc N ; N ; WS |= [] ex?  .)

(mod BAKERY-SATISFACTION2-ABS is
  pr BAKERY-SATISFACTION2 .

  var PS : ProcSet .  vars N M L : Name . var IS : ProcIdleSet .

--- bisimilar equational abstraction
  eq s s s N M ; M ; PS [wait(s N M)] [wait(s s N M)]
   =   s s N M ; M ; PS [wait(s N M)]
  [variant] .
endm)

*** true
(lfmc N ; N ; IS |= [] ex?  .)

