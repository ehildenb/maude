load ../../../contrib/tools/fvp/numbers.maude
load ../../../contrib/tools/meta/purification.maude

--- Module PURIFICATION
--- ===================

select PURIFICATION .

--- Function `subtheoryPurify`
--- --------------------------

reduce subtheoryPurify(asTemplate(upModule('FVP-NAT, true)), upModule('FVP-NAT-MULT, true), '0.Nat)
    == '0.Nat .

reduce subtheoryPurify(asTemplate(upModule('FVP-NAT, true)), upModule('FVP-NAT-MULT, true), 'X:Nat)
    == 'X:Nat .

reduce subtheoryPurify(asTemplate(upModule('FVP-NAT, true)), upModule('FVP-NAT-MULT, true), '_+_['0.Nat, '1.Nat])
    == '_+_['0.Nat, '1.Nat] .

reduce subtheoryPurify(asTemplate(upModule('FVP-NAT, true)), upModule('FVP-NAT-MULT, true), '_+_['0.Nat, 'X:Nat])
    == '_+_['0.Nat, 'X:Nat] .

reduce subtheoryPurify(asTemplate(upModule('FVP-NAT, true)), upModule('FVP-NAT-MULT, true), '_*_['0.Nat, '1.Nat])
    == ('#makeVariable`(_*_`[0.Nat`,1.Nat`]`):Nat st '#makeVariable`(_*_`[0.Nat`,1.Nat`]`):Nat ?= '_*_['0.Nat,'1.Nat]) .

reduce subtheoryPurify(asTemplate(upModule('FVP-NAT, true)), upModule('FVP-NAT-MULT, true), '_+_['_*_['0.Nat, '1.Nat], 'X:Nat])
    == ('_+_['#makeVariable`(_*_`[0.Nat`,1.Nat`]`):Nat,'X:Nat] st '#makeVariable`(_*_`[0.Nat`,1.Nat`]`):Nat ?= '_*_['0.Nat,'1.Nat]) .

reduce subtheoryPurify(asTemplate(upModule('FVP-NAT, true)), upModule('FVP-NAT-MULT, true), '_+_['_*_['0.Nat, '1.Nat], 'X:Nat, '_*_['0.Nat, '1.Nat]])
    == ('_+_['#makeVariable`(_*_`[0.Nat`,1.Nat`]`):Nat,'X:Nat, '#makeVariable`(_*_`[0.Nat`,1.Nat`]`):Nat]
             st '#makeVariable`(_*_`[0.Nat`,1.Nat`]`):Nat ?= '_*_['0.Nat,'1.Nat]) .

fmod ASSOC-LIST { X :: TRIV } is

    sort List{X} NeList{X} .
    ------------------------
    subsort X$Elt < NeList{X} < List{X} .

    var N : X$Elt . var L : NeList{X} .

    op nil :                     ->   List{X} [ctor] .
    op __  : NeList{X} NeList{X} -> NeList{X} [ctor assoc] .
    --------------------------------------------------------

    op head : NeList{X} -> X$Elt .
    ------------------------------
    eq head(N)   = N [variant] .
    eq head(N L) = N [variant] .
endfm

fmod MYLISTNAT is
    protecting FVP-BOOL-CTOR .
    protecting ASSOC-LIST{Nat} *
        (sort NeList{Nat} to NeNatList, sort List{Nat} to NatList) .
endfm

fmod TEST-PURIFY is
    protecting EQFORM .
    protecting PURIFICATION .
    op natMultMod  : -> Module . eq natMultMod  = upModule('FVP-NAT-MULT, true) .
    op natOrderMod : -> Module . eq natOrderMod = upModule('FVP-NAT-PRED, true) .
    op natListMod  : -> Module . eq natListMod  = upModule('MYLISTNAT, true) .

    op deepLeft : -> Term .
    eq deepLeft = '_*_ [ '1.Nat
                       , 'min[ '0.Nat
                             , '_*_ [ '1.Nat
                                    , 'max[ '0.Nat
                                          , '_*_ [ '1.Nat
                                                 , '_+_[ '1.Nat,  '1.Nat]
                       ]     ]      ]     ]      ] .

    op deepRight : -> Term .
    eq deepRight = '_*_ [ 'min[ '_*_ [ 'max[ '_*_ [ '_+_[ '1.Nat,  '1.Nat]
                                                  , '1.Nat ]
                                           , '1.Nat ]
                                     , '1.Nat ]
                              , '1.Nat ]
                        , '1.Nat ] .

    op deepMixed : -> Term .
    eq deepMixed = '_+_[ deepRight, deepLeft ] .
endfm

--- Function `moduleIntersect`
--- --------------------------

reduce moduleIntersect(asTemplate(natMultMod),  asTemplate(natMultMod))  == asTemplate(natMultMod) .
reduce moduleIntersect(asTemplate(natOrderMod), asTemplate(natOrderMod)) == asTemplate(natOrderMod) .
reduce moduleIntersect(asTemplate(natListMod),  asTemplate(natListMod))  == asTemplate(natListMod) .
reduce moduleIntersect(asTemplate(natMultMod),  asTemplate(natOrderMod)) == asTemplate(upModule('FVP-NAT, true)) .
reduce moduleIntersect(asTemplate(natMultMod),  asTemplate(natListMod))  == asTemplate(upModule('FVP-NAT-SORT, true)) .
reduce moduleIntersect(asTemplate(natOrderMod), asTemplate(natListMod))  == asTemplate(upModule('FVP-NAT-SORT, true)) asTemplate(upModule('FVP-BOOL-CTOR, true)) .

reduce 'Foo     inS moduleIntersect(asTemplate(natOrderMod), asTemplate(natListMod)) == false .
reduce 'NatList inS moduleIntersect(asTemplate(natOrderMod), asTemplate(natListMod)) == false .

reduce joint-variable(natListMod, natOrderMod, 'head['L2:NeNatList]) .
reduce joint-variable(natOrderMod, natListMod, 'head['L2:NeNatList]) .
reduce joint-variable(natListMod, natOrderMod, '_<_['N1:Nat, 'N2:Nat]) .

--- Function Purify (Terms)
--- -----------------------

reduce purify(natOrderMod, natListMod, '_<_['head['L1:NeNatList], 'head['L2:NeNatList]]) .
reduce purify(natListMod, natOrderMod, '_<_['head['L1:NeNatList], 'head['L2:NeNatList]]) .
reduce purify(natListMod, natOrderMod,
        '_<_['_+_['head['L':NeNatList],'head['L':NeNatList]]
            ,'_+_['head['L:NeNatList],'head['L:NeNatList]]]) .

reduce purify(natOrderMod, natMultMod, deepLeft ) .
reduce purify(natMultMod, natOrderMod, deepLeft ) .
reduce purify(natOrderMod, natMultMod, deepRight) .
reduce purify(natOrderMod, natMultMod, deepMixed) .

--- Function Purify (EqConjs)
--- -------------------------

reduce purify('MYLISTNAT, 'FVP-NAT-PRED,
        '_<_['head['L1:NeNatList], 'head['L2:NeNatList]] ?= 'true.Bool) .
reduce purify('FVP-NAT-PRED, 'MYLISTNAT,
    'true.Bool != '_<_['_+_['head['L':NeNatList]
                           ,'head['L':NeNatList]]
                      ,'_+_['head['L:NeNatList]
                           ,'head['L:NeNatList]]]) .
reduce purify('MYLISTNAT, 'FVP-NAT-PRED,
    'false.Bool != '_<_['_+_['head['L':NeNatList]
                            ,'head['L':NeNatList]]
                       ,'_+_['head['L:NeNatList]
                            ,'head['L:NeNatList]]]) .
